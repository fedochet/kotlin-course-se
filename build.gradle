plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'antlr'
}

group 'ru.hse.spb'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'ru.hse.spb.parser', '-visitor']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/ru/hse/spb/parser")
}
compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.antlr:antlr4-runtime:4.7"
    antlr "org.antlr:antlr4:4.7"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.hse.spb.MainKt'
    }

    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}